# Main slaspec must define endianess and alignment

define space ram type=ram_space size=4 wordsize=1 default;

define space register type=register_space size=4;

define register offset=0x1000 size=4 [
	r0  r1  r2  r3  r4  r5  r6  r7
	r8  r9 r10 r11 r12  tp  gp  sp
];

define register offset=0x2000 size=4 [
	pc  lp  sar res03
	rpb rpe rpc
];

define register offset=0x00 size=8   contextreg;
define context contextreg
	repeat_end = (0, 4) noflow
	erepeat_end = (4, 8) noflow
#	rpe_ctx = (32, 63) noflow
	rpb_ctx = (32, 63) noflow
;

define token instr(16)
	op0000 = (0, 0)
	op0001 = (0, 1)
	op0003 = (0, 3)
	op0007 = (0, 7)
	op0015 = (0, 15)
	op0102 = (1, 2)
	op0207 = (2, 7)
	op0407 = (4, 7)
	op1215 = (12, 15)
	imm0407 = (4, 7)
	simm6 = (2, 7) signed
	imm0007 = (0, 7)
	simm0007 = (0, 7) signed
	rn = (8, 11)
	rm = (4, 7)
	rl = (0, 3)
	udisp0206 = (2, 6)
	udisp0006 = (0, 6)
	disp7 = (2, 6)
	op0707 = (7, 7)
	op1115 = (11, 15)
	imm5 = (2, 6)
	imm0410 = (4, 10)
	imm0405 = (4, 5)
	op0615 = (6, 15)
	op0815 = (8, 15)
	op0002 = (0, 2)
	imm0307 = (3, 7)
	disp0107 = (1, 7) signed
	disp0111 = (1, 11) signed
	op0103 = (1, 3)
	udisp0106 = (1, 6)
	simm0207 = (2, 7) signed
;

define token ext(16)
	imm1631 = (0, 15)
	simm1631 = (0, 15) signed
;

attach variables [ rn rm rl ] [
	r0  r1  r2  r3  r4  r5  r6  r7
	r8  r9 r10 r11 r12  tp  gp  sp
];

define pcodeop interrupt_enable;
define pcodeop interrupt_disable;
define pcodeop control_bus_load;
define pcodeop control_bus_store;
define pcodeop exception_return;
define pcodeop system_call;
define pcodeop sleep;

Imm16: "#"^imm1631 is imm1631 { export *[const]:4 imm1631; }
Simm16: "#"^simm1631 is simm1631 { export *[const]:4 simm1631; }

# Control register
Creg: regaddr is op0000 & op0407 [ regaddr = ((op0000 << 4) | op0407) * 4 + 0x2000; ] { export *[register]:4 regaddr; }

RepeatTgt: is rpb_ctx {
	export *[ram]:4 rpb_ctx;
}

# Simulate mep delay slot in repeat/erepeat instructions
:^instruction is erepeat_end=2 & instruction [ erepeat_end=1; globalset(inst_next, erepeat_end); erepeat_end=0; globalset(inst_next, rpb_ctx); ] {
	build instruction;
}

:^instruction is erepeat_end=1 & instruction & RepeatTgt [ erepeat_end=0; ] {
	build instruction;
	goto RepeatTgt;
}


:^instruction is repeat_end=2 & instruction [ repeat_end=1; globalset(inst_next, repeat_end); repeat_end=0; globalset(inst_next, rpb_ctx); ] {
	build instruction;
}

:^instruction is repeat_end=1 & instruction & RepeatTgt [ repeat_end=0; ] {
	build instruction;
	rpc = rpc - 1;
	if (rpc == 0) goto <out>;
	goto RepeatTgt;
	<out>
}

#
#
# Instructions
#
#

#
# Repeats, special handling
#

:erepeat end is op0015=0xe019 ; simm1631 [ rpb_ctx = inst_start + 4; end = inst_start + (simm1631 << 1); erepeat_end = 2; globalset(end, erepeat_end); globalset(end, rpb_ctx); ] {
	rpb = inst_next;
	rpe = end;
}

:repeat rn, end is op0007=9 & op1215=0xE & rn ; simm1631 [ rpb_ctx = inst_start + 4; end = inst_start + (simm1631 << 1); repeat_end = 2; globalset(end, repeat_end); globalset(end, rpb_ctx); ] {
	rpc = rn + 1;
}

#
# Arithmetics/logical 2-operand
#

:sub rn, rm is op0003=4 & op1215=0 & rn & rm {
	rn = rn - rm;
}

:or rn, rm is op0003=0 & op1215=1 & rn & rm {
	rn = rn | rm;
}

:and rn, rm is op0003=1 & op1215=1 & rn & rm {
	rn = rn & rm;
}

:xor rn, rm is op0003=2 & op1215=1 & rn & rm {
	rn = rn ^ rm;
}

#
# 3-arg logic
#

:and3 rn, rm, Imm16 is op0003=5 & op1215=0xC & rn & rm ; Imm16 {
	rn = rm & Imm16;
}

:or3 rn, rm, Imm16 is op0003=4 & op1215=0xC & rn & rm ; Imm16 {
	rn = rm | Imm16;
}

:xor3 rn, rm, Imm16 is op0003=6 & op1215=0xC & rn & rm ; Imm16 {
	rn = rm ^ Imm16;
}

#
# Register, Immediate
#

:sll rn, imm0307 is op0002=6 & op1215=6 & rn & imm0307 {
	rn = rn << imm0307;
}

:srl rn, imm0307 is op0002=2 & op1215=6 & rn & imm0307 {
	rn = rn >> imm0307;
}

:sra rn, imm0307 is op0002=3 & op1215=6 & rn & imm0307 {
	rn = rn s>> imm0307;
}

:add rn, simm0207 is op0001=0 & op1215=6 & rn & simm0207 {
	rn = rn + simm0207;
}

#
# Load/Store reg
#

:lb rn, "("^rm^")" is op0003=0xC & op1215=0 & rn & rm {
	rn = sext(*[ram]:1 rm);
}

:lbu rn, "("^rm^")" is op0003=0xB & op1215=0 & rn & rm {
	rn = zext(*[ram]:1 rm);
}

:sb rn, "("^rm^")" is op0003=0x8 & op1215=0 & rn & rm {
	*[ram] rm = rn:1;
}

:lh rn, "("^rm^")" is op0003=0xD & op1215=0 & rn & rm {
	rn = sext(*[ram]:2 rm);
}

:lhu rn, "("^rm^")" is op0003=0xF & op1215=0 & rn & rm {
	rn = zext(*[ram]:2 rm);
}

:sh rn, "("^rm^")" is op0003=0x9 & op1215=0 & rn & rm {
	*[ram] rm = rn:2;
}

:lw rn, "("^rm^")" is op0003=0xE & op1215=0 & rn & rm {
	rn = *[ram]:4 rm;
}

:sw rn, "("^rm^")" is op0003=0xA & op1215=0 & rn & rm {
	*[ram]:4 rm = rn;
}

#
# Load/Store sp-relative
#

:lw rn, off^"(sp)" is op0001=3 & op0707=0 & op1215=4 & rn & udisp0206 [ off = udisp0206 << 2; ] {
	local tgt = sp + off;
	rn = *[ram]:4 tgt;
}

:sw rn, off^"(sp)" is op0001=2 & op0707=0 & op1215=4 & rn & udisp0206 [ off = udisp0206 << 2; ] {
	local tgt = sp + off;
	*[ram]:4 tgt = rn;
}

#
# Load/Store tp-relative
#

:lb rn, udisp0006^"(tp)" is op0707=0 & op1115=0x11 & rn & udisp0006 {
	local tgt = tp + udisp0006;
	rn = sext(*[ram]:1 tgt);
}

:lbu rn, udisp0006^"(tp)" is op0707=1 & op1115=9 & rn & udisp0006 {
	local tgt = tp + udisp0006;
	rn = zext(*[ram]:1 tgt);
}

:sb rn, udisp0006^"(tp)" is op0707=0 & op1115=0x10 & rn & udisp0006 {
	local tgt = tp + udisp0006;
	*[ram] tgt = rn:1;
}

:lh rn, off^"(tp)" is op0000=0 & op0707=1 & op1115=0x11 & rn & udisp0106 [ off = udisp0106 << 1; ] {
	local tgt = tp + off;
	rn = sext(*[ram]:2 tgt);
}

:lhu rn, off^"(tp)" is op0000=1 & op0707=1 & op1115=0x11 & rn & udisp0106 [ off = udisp0106 << 1; ] {
	local tgt = tp + off;
	rn = zext(*[ram]:2 tgt);
}

:sh rn, off^"(tp)" is op0000=0 & op0707=1 & op1115=0x10 & rn & udisp0106 [ off = udisp0106 << 1; ] {
	local tgt = tp + off;
	*[ram] tgt = rn:2;
}

:lw rn, off^"(tp)" is op0001=3 & op0707=1 & op1115=8 & rn & udisp0206 [ off = udisp0206 << 2; ] {
	local tgt = tp + off;
	rn = *[ram]:4 tgt;
}

:sw rn, off^"(tp)" is op0001=2 & op0707=1 & op1115=8 & rn & udisp0206 [ off = udisp0206 << 2; ] {
	local tgt = tp + off;
	*[ram]:4 tgt = rn;
}

#
# Load/Store disp16
#

:lb rn, simm1631^"("^rm^")" is op0003=0xC & op1215=0xC & rn & rm ; simm1631 {
	local tgt = rm + simm1631;
	rn = sext(*[ram]:1 tgt);
}

:lbu rn, simm1631^"("^rm^")" is op0003=0xB & op1215=0xC & rn & rm ; simm1631 {
	local tgt = rm + simm1631;
	rn = zext(*[ram]:1 tgt);
}

:sb rn, simm1631^"("^rm^")" is op0003=0x8 & op1215=0xC & rn & rm ; simm1631 {
	local tgt = rm + simm1631;
	*[ram] tgt = rn:1;
}

:lh rn, simm1631^"("^rm^")" is op0003=0xD & op1215=0xC & rn & rm ; simm1631 {
	local tgt = rm + simm1631;
	rn = sext(*[ram]:2 tgt);
}

:lhu rn, simm1631^"("^rm^")" is op0003=0xF & op1215=0xC & rn & rm ; simm1631 {
	local tgt = rm + simm1631;
	rn = zext(*[ram]:2 tgt);
}

:sh rn, simm1631^"("^rm^")" is op0003=0x9 & op1215=0xC & rn & rm ; simm1631 {
	local tgt = rm + simm1631;
	*[ram] tgt = rn:2;
}

:lw rn, simm1631^"("^rm^")" is op0003=0xE & op1215=0xC & rn & rm ; simm1631 {
	local tgt = rm + simm1631;
	rn = *[ram]:4 tgt;
}

:sw rn, simm1631^"("^rm^")" is op0003=0xA & op1215=0xC & rn & rm ; simm1631 {
	local tgt = rm + simm1631;
	*[ram]:4 tgt = rn;
}

#
# Load/Store abs24
#

:lw rn, abs24 is op0001=3 & op1215=0xE & rn & op0207 ; imm1631 [ abs24 = (imm1631 << 8) | (op0207 << 2); ] {
	rn = *[ram]:4 abs24:4;
}

:sw rn, abs24 is op0001=2 & op1215=0xE & rn & op0207 ; imm1631 [ abs24 = (imm1631 << 8) | (op0207 << 2); ] {
	*[ram]:4 abs24:4 = rn;
}

#
# Special arithmetics
#

:nor rn, rm is op0003=3 & op1215=1 & rn & rm {
	rn = ~(rn | rm);
}

:neg rn, rm is op0003=1 & op1215=0 & rn & rm {
	rn = -rm;
}

:abs rn, rm is op0003=1 & op1215=0xF & rn & rm ; imm1631=3 {
	local tmp = (rn - rm);
	if (tmp > 0) goto <skip>;
	tmp = -tmp;
	<skip>
	rn = tmp;
}

#
# Move instructions
#

:mov rn, rm is op0003=0 & op1215=0 & rn & rm {
	rn = rm;
}

:mov rn, simm0007 is op1215=5 & rn & simm0007 {
	rn = simm0007;
}

:mov rn, simm1631 is op0007=1 & op1215=0xC & rn ; simm1631 {
	rn = simm1631;
}

:movh rn, imm1631 is op0007=0x21 & op1215=0xC & rn ; imm1631 {
	rn = imm1631 << 16;
}

:movu rn, imm1631 is op0007=0x11 & op1215=0xC & rn ; imm1631 {
	rn = imm1631;
}

:movu rn, imm24 is op1115=0x1A & rn & imm0007 ; imm1631 [ imm24 = imm0007 | (imm1631 << 8); ] {
	rn = imm24;
}

#
# Load/store control reg
#

:ldc rn, Creg is op0103=5 & op1215=7 & rn & Creg {
	rn = Creg;
}

:stc rn, Creg is op0103=4 & op1215=7 & rn & Creg {
	Creg = rn;
}

#
# slt* instructions
#

:slt3 "r0", rn, rm is op0003=2 & op1215=0 & rn & rm {
	r0 = zext(rn s< rm);
}

:sltu3 "r0", rn, rm is op0003=3 & op1215=0 & rn & rm {
	r0 = zext(rn < rm);
}

:slt3 "r0", rn, imm0307 is op0002=1 & op1215=6 & rn & imm0307 {
	r0 = zext(rn s< imm0307);
}

:sltu3 "r0", rn, imm0307 is op0002=5 & op1215=6 & rn & imm0307 {
	r0 = zext(rn < imm0307);
}

:slt3 rn, rm, imm1631 is op0003=2 & op1215=0xC & rn & rm ; imm1631 {
	rn = zext(rm s< imm1631);
}

:sltu3 rn, rm, imm1631 is op0003=3 & op1215=0xC & rn & rm ; imm1631 {
	rn = zext(rm < imm1631);
}

#
# Conditional branches
#

Target0107: target is disp0107 [ target = inst_start + (disp0107 << 1); ] {
	export *:4 target;
}

:bne rn, rm, tgt is op0003=5 & op1215=0xE & rn & rm ; simm1631 [ tgt = inst_start + (simm1631 << 1); ] {
	if (rn == rm) goto <skip>;
	goto [tgt:4];
	<skip>
}

:beq rn, rm, tgt is op0003=1 & op1215=0xE & rn & rm ; simm1631 [ tgt = inst_start + (simm1631 << 1); ] {
	if (rn != rm) goto <skip>;
	goto [tgt:4];
	<skip>
}

:bnez rn, Target0107 is op0000=1 & op1215=0xA & rn & Target0107 {
	if (rn == 0) goto <skip>;
	goto Target0107;
	<skip>
}

:beqz rn, Target0107 is op0000=0 & op1215=0xA & rn & Target0107 {
	if (rn != 0) goto <skip>;
	goto Target0107;
	<skip>
}

:bnei rn, imm0407, tgt is op0003=4 & op1215=0xE & rn & imm0407 ; simm1631 [ tgt = inst_start + (simm1631 << 1); ] {
	if (rn == imm0407) goto <skip>;
	pc = tgt;
	goto [pc];
	<skip>
}

:beqi rn, imm0407, tgt is op0003=0 & op1215=0xE & rn & imm0407 ; simm1631 [ tgt = inst_start + (simm1631 << 1); ] {
	if (rn != imm0407) goto <skip>;
	pc = tgt;
	goto [pc];
	<skip>
}

:blti rn, imm0407, tgt is op0003=0xC & op1215=0xE & rn & imm0407 ; simm1631 [ tgt = inst_start + (simm1631 << 1); ] {
	if (rn s>= imm0407) goto <skip>;
	pc = tgt;
	goto [pc];
	<skip>
}

:bgei rn, imm0407, tgt is op0003=0x8 & op1215=0xE & rn & imm0407 ; simm1631 [ tgt = inst_start + (simm1631 << 1); ] {
	if (rn s< imm0407) goto <skip>;
	pc = tgt;
	goto [pc];
	<skip>
}

#
# Jumps/branches/function calls
#

:bsr tgt is op0000=1 & op1215=0xB & disp0111 [ tgt = inst_start + (disp0111 << 1); ] {
	lp = inst_next;
	call [tgt:4];
}

:bsr tgt is op0003=9 & op1115=0x1B & imm0410 ; simm1631 [ tgt = inst_start + (((imm0410 << 1) | (simm1631 << 8))); ] {
	lp = inst_next;
	call [tgt:4];
}

:jsr rm is op0003=0xF & op0815=0x10 & rm {
	lp = inst_next;
	call [rm];
}

# Jumps to $11 and $1 are returns (in vita code)
:jmp rm is op0003=0xE & op0815=0x10 & rm & (rm=1 | rm=11) {
	return [rm];
}

# Other jumps are just branches
:jmp rm is op0003=0xE & op0815=0x10 & rm {
	goto [rm];
}

:jmp tgt is op0003=8 & op1115=0x1B & imm0410 ; imm1631 [ tgt = (inst_start & 0xF0000000) | (imm1631 << 8) | (imm0410 << 1); ] {
	pc = tgt;
	goto [pc];
}

:bra tgt is op0000=0 & op1215=0xB & disp0111 [ tgt = inst_start + (disp0111 << 1); ] {
	goto [tgt:4];
}

#
# add3
#

:add3 rn, rm, Simm16 is op0003=0 & op1215=0xC & rn & rm ; Simm16 {
	rn = rm + sext(Simm16:2);
}

:add3 rl, rn, rm is op1215=9 & rl & rn & rm {
	rl = rn + rm;
}

:add3 rn, "sp", imm is op0001=0 & op0707=0 & op1215=4 & rn & imm5 [ imm = imm5 << 2; ]  {
	rn = sp + imm;
}

#
# sll/srl with reg
#

:sll rn, rm is op0003=0xE & op1215=2 & rn & rm {
	rn = rn << (rm & 0x1F);
}

:srl rn, rm is op0003=0xC & op1215=2 & rn & rm {
	rn = rn >> (rm & 0x1F);
}

#
# sll3
#

:sll3 "r0", rn, imm0307 is op0002=7 & op1215=6 & rn & imm0307 {
	r0 = rn << imm0307;
}

#
# Unsigned/signed extends
#

:extb rn is op0007=0xD & op1215=1 & rn {
	rn = sext(rn:1);
}

:extub rn is op0007=0x8D & op1215=1 & rn {
	rn = zext(rn:1);
}

:exth rn is op0007=0x2D & op1215=1 & rn {
	rn = sext(rn:2);
}

:extuh rn is op0007=0xAD & op1215=1 & rn {
	rn = zext(rn:2);
}

#
# ret
#

:ret is op0015=0x7002 {
	pc = lp;
	return [pc];
}

#
# System instructions
#

:ei is op0015=0x7010 {
	interrupt_enable();
}

:di is op0015=0x7000 {
	interrupt_disable();
}

:reti is op0015=0x7012 {
	exception_return();
}

:ldcb rn, imm1631 is op0007=0x14 & op1215=0xF & rn ; imm1631 {
	rn = control_bus_load(imm1631:4);
}

:stcb rn, imm1631 is op0007=4 & op1215=0xF & rn ; imm1631 {
	control_bus_store(imm1631:4, rn);
}

:swi imm0405 is op0003=6 & op0615=0x1C0 & imm0405 {
# This is vita-specific
# TODO: check it's actually correct
	r0 = system_call(imm0405:4, r1, r2, r3, r4);
}

:sleep is op0015=0x7062 {
	sleep();
}
